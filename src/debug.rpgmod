**FREE

ctl-opt nomain thread(*concurrent);


/include 'debug.rpginc'
/include 'message.rpginc'


dcl-s enabled ind inz(*off) static(*allthread);
dcl-s internal_log pointer(*proc) static(*allthread);

dcl-proc iv_debug export;
    dcl-pi *n opdesc;
        message pointer value options(*string);
        replaceValue1 pointer value options(*string : *nopass);
        replaceValue2 pointer value options(*string : *nopass);
        replaceValue3 pointer value options(*string : *nopass);
        replaceValue4 pointer value options(*string : *nopass);
        replaceValue5 pointer value options(*string : *nopass);
        replaceValue6 pointer value options(*string : *nopass);
        replaceValue7 pointer value options(*string : *nopass);
    end-pi;
  
    dcl-pr log extproc(internal_log) opdesc;
        message pointer value options(*string);
        replaceValue1 pointer value options(*string : *nopass);
        replaceValue2 pointer value options(*string : *nopass);
        replaceValue3 pointer value options(*string : *nopass);
        replaceValue4 pointer value options(*string : *nopass);
        replaceValue5 pointer value options(*string : *nopass);
        replaceValue6 pointer value options(*string : *nopass);
        replaceValue7 pointer value options(*string : *nopass);
    end-pr;

    if (enabled);
        if (%parms() = 1);
            log(message);
        elseif (%parms() = 2);
            log(message : replaceValue1);
        elseif (%parms() = 3);
            log(message : replaceValue1 : replaceValue2);
        elseif (%parms() = 4);
            log(message : replaceValue1 : replaceValue2 : replaceValue3);
        elseif (%parms() = 5);
            log(message : replaceValue1 : replaceValue2 : replaceValue3 : replaceValue4);
        elseif (%parms() = 6);
            log(message : replaceValue1 : replaceValue2 : replaceValue3 : replaceValue4 : replaceValue5);
        elseif (%parms() = 7);
            log(message : replaceValue1 : replaceValue2 : replaceValue3 : replaceValue4 : replaceValue5 : replaceValue6);
        elseif (%parms() = 8);
            log(message : replaceValue1 : replaceValue2 : replaceValue3 : replaceValue4 : replaceValue5 : replaceValue6 : replaceValue7);
        endif;
    endif;
end-proc;


dcl-proc iv_debug_init export;
    dcl-pi *n int(10) end-pi;

    dcl-pr getenv pointer extproc('getenv');
        *n pointer value options(*string : *trim);
    end-pr;
  
    dcl-s value pointer;
  
    value = getenv('ILEVATOR_DEBUG');
    if (value <> *null and %str(value) = *on);
        internal_log = %paddr(iv_debug_joblog);
        enabled = *on;
        message_info('ILEvator debug logging enabled');
    endif;
  
    return 1;
end-proc;


dcl-proc iv_debug_joblog;
    dcl-pi *n;
        message pointer value options(*string);
        replaceValue1 pointer value options(*string : *nopass);
        replaceValue2 pointer value options(*string : *nopass);
        replaceValue3 pointer value options(*string : *nopass);
        replaceValue4 pointer value options(*string : *nopass);
        replaceValue5 pointer value options(*string : *nopass);
        replaceValue6 pointer value options(*string : *nopass);
        replaceValue7 pointer value options(*string : *nopass);
    end-pi;

    if (%parms() = 1);
        message_info(message);
    elseif (%parms() = 2);
        message_info(message : replaceValue1);
    elseif (%parms() = 3);
        message_info(message : replaceValue1 : replaceValue2);
    elseif (%parms() = 4);
        message_info(message : replaceValue1 : replaceValue2 : replaceValue3);
    elseif (%parms() = 5);
        message_info(message : replaceValue1 : replaceValue2 : replaceValue3 : replaceValue4);
    elseif (%parms() = 6);
        message_info(message : replaceValue1 : replaceValue2 : replaceValue3 : replaceValue4 : replaceValue5);
    elseif (%parms() = 7);
        message_info(message : replaceValue1 : replaceValue2 : replaceValue3 : replaceValue4 : replaceValue5 : replaceValue6);
    elseif (%parms() = 8);
        message_info(message : replaceValue1 : replaceValue2 : replaceValue3 : replaceValue4 : replaceValue5 : replaceValue6 : replaceValue7);
    endif;
end-proc;
