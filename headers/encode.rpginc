**FREE

/if not defined (ILEVATOR_ENCODE)
/define ILEVATOR_ENCODE

///
// MIME encoding base64
///
dcl-c IV_ENCODE_MIME_BASE64 'B';

///
// MIME encoding character set UTF-8
///
dcl-c IV_ENCODE_MIME_UTF8 'UTF-8';

///
// ILEvator : Encoding Module
//
// This module contains the procedures for url encoding and form url encoding
// values. URL encoding is also known as percent encoding. Form URL encoding
// is almost the same as percent encoding with the difference that the space
// character is represented as a plus sign.
//
// @author Mihael Schmidt
// @project ILEvator
//
///


///
// URL encode value
//
// URL encodes the passed value. The input string can contain single and 
// multibyte characters.
//
// @param Input string
// @return URL encoded string
///
dcl-pr iv_encode_url varchar(4194304) ccsid(*utf8) rtnparm extproc(*dclcase);
    unencodedValue varchar(1048576) ccsid(*utf8) value; 
end-pr;

///
// Form URL encode value
//
// Form URL encodes the passed value. The input string can contain single and 
// multibyte characters. The space sign will be encoded as a plus sign.
//
// @param Input string
// @return Form URL encoded string
///
dcl-pr iv_encode_formUrl varchar(4194304) ccsid(*utf8) rtnparm extproc(*dclcase);
    unencodedValue varchar(1048576) ccsid(*utf8) value;
end-pr;

///
// Encode URL path element
//
// URL encodes the passed value so that it can be safely used as a path element
// in a URL.
//
// @param Input string
// @return URL encoded string
///
dcl-pr iv_encode_path varchar(32766) ccsid(*utf8) extproc(*dclcase);
    unencodedValue varchar(32766) ccsid(*utf8) value; 
end-pr;

///
// Encode URL query element
//
// URL encodes the passed key and value part of a query string and concats them
// with an equal sign so that it can be safely used as a URL query parameter.
//
// @param Query parameter key
// @param Query parameter value
// @return URL encoded query parameter: encoded_key=encoded_value
///
dcl-pr iv_encode_query varchar(32766) ccsid(*utf8) extproc(*dclcase);
    key varchar(1000) ccsid(*utf8) value;
    value varchar(32766) ccsid(*utf8) value;
end-pr;

///
// Encode with Punycode 
//
// The passed value will be punycode encoded.
//
// @param Value to be encoded
// @return Encoded value
//
// @info This is only available if the ICU service programs are installed.
// @info Punycode RFC 3492
///
dcl-pr iv_encode_punycode varchar(32766) extproc(*dclcase);
    value varchar(32766) const;
end-pr;

///
// Encode as ACE string
//
// The passed value will be punycode encoded and returned as an ACE string.
//
// @param Value to be encoded
// @return Encoded value
//
// @info This is only available if the ICU service programs are installed.
// @info ACE string (ACE = ASCII Compatible Encoding), see RFC 5890.
///
dcl-pr iv_encode_ace varchar(32766) extproc(*dclcase);
    value varchar(32766) const;
end-pr;

///
// MIME encode string
//
// The passed value will be mime encoded. This procedure only supports
// base64 at the moment.
//
// @param Value to be encoded
// @param Character set of the passed value (default: IV_ENCODE_MIME_UTF8)
// @param Encoding scheme (default: IV_ENCODE_MIME_BASE64)
// @return MIME encoded value
///
dcl-pr iv_encode_mime varchar(524284) ccsid(*hex) extproc(*dclcase);
    data varchar(524284) ccsid(*hex) value;
    charset varchar(20) const options(*nopass : *omit);
    encoding char(1) value options(*nopass);
end-pr;

/endif
