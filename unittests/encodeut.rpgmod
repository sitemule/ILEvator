**FREE

///
// Encode Unit Test
//
// @author Mihael Schmidt
// @date   23.03.2023
///


ctl-opt nomain;


/include 'assert.rpginc'
/include 'encode.rpginc'
/include 'teraspace.rpginc'

dcl-pr tearDown extproc(*dclcase) end-pr;
dcl-pr test_urlEncodeAsciiOnly end-pr;
dcl-pr test_urlEncodeInvariantChars end-pr;
dcl-pr test_urlEncodeUmlaute end-pr;
dcl-pr test_formUrlEncodeAsciiOnly end-pr;
dcl-pr test_formUrlEncodeInvariantChars end-pr;
dcl-pr test_formUrlEncodeUmlaute end-pr;
dcl-pr test_encodePathWithPlusAndSpace end-pr;
dcl-pr test_encodeQueryWithPlusAndSpace end-pr;
dcl-pr test_punycode end-pr;
dcl-pr test_punycodeOnlyAscii end-pr;
dcl-pr test_punycodeEmpty end-pr;
dcl-pr test_ace end-pr;
dcl-pr test_aceOnlyAscii end-pr;
dcl-pr test_aceEmpty end-pr;
dcl-pr test_aceSubDomains end-pr;
dcl-pr test_aceStartsWithPoint end-pr;
dcl-pr test_aceEndsWithPoint end-pr;
dcl-pr test_aceDanischChars end-pr;
dcl-pr test_encodeMimeBase64 end-pr;
dcl-pr test_encodeBase64 end-pr;
dcl-pr test_encodeBase64Buffer end-pr;


dcl-proc tearDown export;
    if (teraspace_leak() = 1);
        dsply %trimr('Leaking memory: ' + %char(teraspace_use()));
    endif;
end-proc;


dcl-proc test_urlEncodeAsciiOnly export;
  dcl-s encodedValue varchar(4194304) ccsid(*utf8);
  
  encodedValue = iv_encode_url('sitemule.com');
  
  aEqual('sitemule.com' : encodedValue);
end-proc;


dcl-proc test_urlEncodeInvariantChars export;
  dcl-s encodedValue varchar(4194304) ccsid(*utf8);
  
  encodedValue = iv_encode_url('Question Mark:?');
  aEqual('Question%20Mark%3A%3F' : encodedValue);
end-proc;


dcl-proc test_urlEncodeUmlaute export;
  dcl-s encodedValue varchar(4194304) ccsid(*utf8);

  // ae and UE
  encodedValue = iv_encode_url('Umlaute:' + u'00E4' + u'00DC');
  aEqual('Umlaute%3A%C3%A4%C3%9C' : encodedValue);
end-proc;


dcl-proc test_formUrlEncodeAsciiOnly export;
  dcl-s encodedValue varchar(4194304) ccsid(*utf8);
  
  encodedValue = iv_encode_formUrl('sitemule.com');
  
  aEqual('sitemule.com' : encodedValue);
end-proc;


dcl-proc test_formUrlEncodeInvariantChars export;
  dcl-s encodedValue varchar(4194304) ccsid(*utf8);
  
  encodedValue = iv_encode_formUrl('Question Mark:?');
  aEqual('Question+Mark%3A%3F' : encodedValue);
end-proc;


dcl-proc test_formUrlEncodeUmlaute export;
  dcl-s encodedValue varchar(4194304) ccsid(*utf8);

  // ae and UE
  encodedValue = iv_encode_formUrl('Umlaute:' + u'00E4' + u'00DC');
  aEqual('Umlaute%3A%C3%A4%C3%9C' : encodedValue);
end-proc;


dcl-proc test_encodePathWithPlusAndSpace export;
  dcl-s encodedValue varchar(32766) ccsid(*utf8);
  
  encodedValue = iv_encode_path('Niels+J' + u'00F8' + 'rn and Mihael');
  aEqual('Niels%2BJ%C3%B8rn%20and%20Mihael' : encodedValue);
end-proc;


dcl-proc test_encodeQueryWithPlusAndSpace export;
  dcl-s encodedValue varchar(32766) ccsid(*utf8);
  
  encodedValue = iv_encode_query('names' : 'Niels+J' + u'00F8' + 'rn and Mihael');
  aEqual('names=Niels%2BJ%C3%B8rn%20and%20Mihael' : encodedValue);
end-proc;


dcl-proc test_punycode export;
  dcl-s encodedValue varchar(32766);
  
  encodedValue = iv_encode_punycode('k' + u'00FC' + 'chen.de');
  aEqual('kchen.de-65a' : encodedValue);
end-proc;


dcl-proc test_punycodeOnlyAscii export;
  dcl-s encodedValue varchar(32766);
  
  encodedValue = iv_encode_punycode('kuechen.de');
  aEqual('kuechen.de' : encodedValue);
end-proc;


dcl-proc test_punycodeEmpty export;
  dcl-s encodedValue varchar(32766);
  
  encodedValue = iv_encode_punycode('');
  aEqual('' : encodedValue);
end-proc;


dcl-proc test_ace export;
  dcl-s encodedValue varchar(32766);
  
  encodedValue = iv_encode_ace('k' + u'00FC' + 'chen.de');
  aEqual('xn--kchen-kva.de' : encodedValue);
end-proc;


dcl-proc test_aceOnlyAscii export;
  dcl-s encodedValue varchar(32766);
  
  encodedValue = iv_encode_ace('kuechen.de');
  aEqual('kuechen.de' : encodedValue);
end-proc;


dcl-proc test_aceEmpty export;
  dcl-s encodedValue varchar(32766);
  
  encodedValue = iv_encode_ace('');
  aEqual('' : encodedValue);
end-proc;


dcl-proc test_aceSubDomains export;
  dcl-s encodedValue varchar(32766);
  
  encodedValue = iv_encode_ace('j' + u'00F6' + 'rg.m' + u'00FC' + 'hle.de');
  aEqual('xn--jrg-sna.xn--mhle-0ra.de' : encodedValue);
end-proc;


dcl-proc test_aceStartsWithPoint export;
  dcl-s encodedValue varchar(32766);
  
  encodedValue = iv_encode_ace('.de');
  aEqual('.de' : encodedValue);
end-proc;


dcl-proc test_aceEndsWithPoint export;
  dcl-s encodedValue varchar(32766);
  
  encodedValue = iv_encode_ace('m' + u'00FC' + 'hle.');
  aEqual('xn--mhle-0ra.' : encodedValue);
end-proc;


dcl-proc test_aceDanischChars export;
  dcl-s encodedValue varchar(32766);

  encodedValue = iv_encode_ace('K' + u'00F8' + 'kken-V' + u'00E6' + 'rkt' + u'00F8' + 'jer.dk');
  aEqual('xn--Kkken-Vrktjer-9fb7xka.dk' : encodedValue);
end-proc;


dcl-proc test_encodeMimeBase64 export;
  dcl-s encodedValue varchar(524284) ccsid(*utf8);
  
  encodedValue = iv_encode_mime('J' + u'00F6' + 'rg M' + u'00FC' + 'ller');
  
  assert('=?UTF-8?B?SsO2cmcgTcO8bGxlcg==?=' = encodedValue : 'MIME encoded value does not match');
end-proc;

dcl-proc test_encodeBase64 export;
  dcl-s encodedValue varchar(524284) ccsid(*utf8);
  
  encodedValue = iv_encode_base64('J' + u'00F6' + 'rg M' + u'00FC' + 'ller');
  assert('SsO2cmcgTcO8bGxlcg==' = encodedValue : 'Base64 encoded value does not match');
end-proc;

dcl-proc test_encodeBase64Buffer export;
  dcl-s encodedValue varchar(524284) ccsid(*utf8);
  dcl-s input varchar(100) ccsid(*utf8);
  
  input = 'J' + u'00F6' + 'rg M' + u'00FC' + 'ller';
  
  %len(encodedValue) = 524284;
  %len(encodedValue) = iv_encode_base64_buffer(%addr(encodedValue : *data) : %addr(input : *data) : %len(input));
  assert('SsO2cmcgTcO8bGxlcg==' = encodedValue : 'Base64 encoded value does not match');
end-proc;
