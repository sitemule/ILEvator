**FREE

///
// Multipart Unit Test
//
// @project ILEvator
// @author Mihael Schmidt
// @date   07.09.2023
///


ctl-opt nomain;


/include 'api.rpginc'
/include 'assert.rpginc'
/include 'multipart.rpginc'
/include 'stream.rpginc'

dcl-pr splitLines int(20) extproc('SPLITLINES');
    buffer char(479232);
    bufferLength int(20) const;
    lastBlock ind const;
end-pr;

dcl-c O_RDONLY 1;
dcl-c O_WRONLY 2;
dcl-c O_CREATE 8;
dcl-c S_IRUSR 256;
dcl-c S_IWUSR 128;

dcl-pr open int(10) extproc('open');
  path pointer value options(*string);
  openflags int(10) value;
  mode uns(10) value options(*nopass);
  ccsid uns(10) value options(*nopass);
end-pr;

dcl-pr read int(10) extproc('read');
  fildes int(10) value;
  buf pointer value;
  bytes uns(10) value;
end-pr;

dcl-pr write int(10) extproc('write');
    fileDesc int(10) value;
    buffer pointer value;
    length uns(10) value;
end-pr;

dcl-pr close int(10) extproc('close');
  fildes int(10) value;
end-pr;

dcl-pr memcmp int(10) extproc('memcmp');
  buffer1 pointer value;
  buffer2 pointer value;
  count uns(10) value;
end-pr;

dcl-pr memcpy pointer extproc('memcpy');
  target pointer value;
  source pointer value;
  count uns(10) value;
end-pr;

dcl-pr getenv pointer extproc('getenv');
    envvar pointer value options(*string : *trim);
end-pr;

dcl-s stream_buffer_t varchar(10000) ccsid(*utf8) template;


dcl-pr test_singleTextPart end-pr;
dcl-pr test_singleTextPartWithHeaders end-pr;
dcl-pr test_multipleTextParts end-pr;
dcl-pr test_singleTextBase64Encoded end-pr;
dcl-pr test_umlautFieldName end-pr;
dcl-pr test_singleBytesPart end-pr;
dcl-pr test_singleFilePartBinary end-pr;
dcl-pr test_singleFilePartBase64 end-pr;
dcl-pr test_singleTextPartBase64 end-pr;
dcl-pr test_splitLinesOnBigFile end-pr;

dcl-c BOUNDARY 'ilevator-unittest-boundary';

dcl-s projectSourceDir varchar(1000);


dcl-proc setUpSuite export;
    dcl-s value pointer;
    
    value = getenv('ILEVATOR_SOURCE_DIR');
    if (value <> *null);
        projectSourceDir = %str(value : %size(projectSourceDir));
    endif;
end-proc;


dcl-proc test_singleTextPart export;
    dcl-s multipart pointer;
    dcl-s assumedResult varchar(1000000:4) ccsid(*utf8);
    dcl-ds outstream likeds(stream_t) based(p_outstream);
    dcl-s buffer like(stream_buffer_t);
    
    assumedResult = loadFile('unittests/multipart/test1-single-text-part.txt');
    
    p_outstream = stream_new(%size(stream_buffer_t));
    outstream.writer = %paddr(stream_memory_writer);
    outstream.handle = %addr(buffer);
    
    multipart = iv_multipart_new(IV_MULTIPART_MEDIA_TYPE : BOUNDARY);
    iv_multipart_addPartFromString(multipart : 'test' : 'ILEvator rocks!');
    iv_multipart_finalize(multipart);
    
    iv_multipart_toStream(multipart : p_outstream);
    
    iEqual(%len(assumedResult) : %len(buffer));
    assert(memcmp(%addr(assumedResult : *data) : %addr(buffer : *data) : %len(buffer)) = 0: 
        'Multipart does not correspond with the assumed result.');
    
    on-exit;
        iv_multipart_free(multipart);
        stream_delete(p_outstream);
end-proc;


dcl-proc test_singleTextPartBase64 export;
    dcl-s multipart pointer;
    dcl-s assumedResult varchar(1000000:4) ccsid(*utf8);
    dcl-ds outstream likeds(stream_t) based(p_outstream);
    dcl-s buffer like(stream_buffer_t);
        
    
    assumedResult = loadFile('unittests/multipart/test8-single-text-part-base64.txt');
    
    p_outstream = stream_new(%size(stream_buffer_t));
    outstream.writer = %paddr(stream_memory_writer);
    outstream.handle = %addr(buffer);
    
    multipart = iv_multipart_new(IV_MULTIPART_MEDIA_TYPE : BOUNDARY);
    iv_multipart_addPartFromString(multipart : 'test' :
        'Lorem ipsum dolor sit amet, consectetur adipiscing elit, sed do eiusmod tempor incididunt +
        ut labore et dolore magna aliqua. Integer enim neque volutpat ac. Turpis massa sed elementum +
        tempus. Purus sit amet luctus venenatis lectus magna. Aliquet bibendum enim facilisis gravida +
        neque convallis. Porta lorem mollis aliquam ut porttitor leo a diam. Diam in arcu cursus euismod +
        quis viverra nibh. Nunc aliquet bibendum enim facilisis gravida neque. Id donec ultrices tincidunt +
        arcu. Feugiat pretium nibh ipsum consequat nisl vel pretium lectus quam. Aenean sed adipiscing diam +
        donec.' :
        IV_MULTIPART_ENCODING_BASE64);
    iv_multipart_finalize(multipart);
    
    iv_multipart_toStream(multipart : p_outstream);
    
    iEqual(%len(assumedResult) : %len(buffer));
    assert(memcmp(%addr(assumedResult : *data) : %addr(buffer : *data) : %len(buffer)) = 0: 
        'Multipart does not correspond with the assumed result.');
    
    on-exit;
        iv_multipart_free(multipart);
        stream_delete(p_outstream);
end-proc;


dcl-proc test_singleBytesPart export;
    dcl-s multipart pointer;
    dcl-s assumedResult varchar(1000000:4) ccsid(*utf8);
    dcl-ds outstream likeds(stream_t) based(p_outstream);
    dcl-s buffer like(stream_buffer_t);
    dcl-s content varchar(100) ccsid(*utf8);
        
    content = 'ILEvator rocks' + u'0021';
    
    assumedResult = loadFile('unittests/multipart/test5-single-bytes-part.txt');
    
    p_outstream = stream_new(%size(stream_buffer_t));
    outstream.writer = %paddr(stream_memory_writer);
    outstream.handle = %addr(buffer);
    
    multipart = iv_multipart_new(IV_MULTIPART_MEDIA_TYPE : BOUNDARY);
    iv_multipart_addPartFromBytes(multipart : 'test' : %addr(content : *data) : %len(content));
    iv_multipart_finalize(multipart);
    
    iv_multipart_toStream(multipart : p_outstream);
    
    iEqual(%len(assumedResult) : %len(buffer));
    assert(memcmp(%addr(assumedResult : *data) : %addr(buffer : *data) : %len(buffer)) = 0: 
        'Multipart does not correspond with the assumed result.');
    
    on-exit;
        iv_multipart_free(multipart);
        stream_delete(p_outstream);
end-proc;


dcl-proc test_singleFilePartBinary export;
    dcl-s multipart pointer;
    dcl-s assumedResult varchar(1000000:4) ccsid(*utf8);
    dcl-ds outstream likeds(stream_t) based(p_outstream);
    dcl-s buffer like(stream_buffer_t);
    
    assumedResult = loadFile('unittests/multipart/test6-single-file-part.txt');
    
    p_outstream = stream_new(%size(stream_buffer_t));
    outstream.writer = %paddr(stream_memory_writer);
    outstream.handle = %addr(buffer);
    
    multipart = iv_multipart_new(IV_MULTIPART_MEDIA_TYPE : BOUNDARY);
    iv_multipart_addPartFromFile(multipart : 'test' : 'unittests/multipart/test6-input.txt' : 'test6-input.txt' : IV_MULTIPART_ENCODING_BINARY);
    iv_multipart_finalize(multipart);
    
    iv_multipart_toStream(multipart : p_outstream);
    
    iEqual(%len(assumedResult) : %len(buffer));
    assert(memcmp(%addr(assumedResult : *data) : %addr(buffer : *data) : %len(buffer)) = 0: 
        'Multipart does not correspond with the assumed result.');
    
    on-exit;
        iv_multipart_free(multipart);
        stream_delete(p_outstream);
end-proc;


dcl-proc test_singleFilePartBase64 export;
    dcl-s multipart pointer;
    dcl-s assumedResult varchar(1000000:4) ccsid(*utf8);
    dcl-ds outstream likeds(stream_t) based(p_outstream);
    dcl-s buffer like(stream_buffer_t);
        
    assumedResult = loadFile('unittests/multipart/test7-single-file-part-base64.txt');
    
    p_outstream = stream_new(%size(stream_buffer_t));
    outstream.writer = %paddr(stream_memory_writer);
    outstream.handle = %addr(buffer);
    
    multipart = iv_multipart_new(IV_MULTIPART_MEDIA_TYPE : BOUNDARY);
    iv_multipart_addPartFromFile(multipart : 'test' : 'unittests/multipart/test7-input.txt');
    iv_multipart_finalize(multipart);
    
    iv_multipart_toStream(multipart : p_outstream);
    
    iEqual(%len(assumedResult) : %len(buffer));
    assert(memcmp(%addr(assumedResult : *data) : %addr(buffer : *data) : %len(buffer)) = 0: 
        'Multipart does not correspond with the assumed result.');
    
    on-exit;
        iv_multipart_free(multipart);
        stream_delete(p_outstream);
end-proc;


dcl-proc test_umlautFieldName export;
    dcl-s multipart pointer;
    dcl-s assumedResult varchar(1000000:4) ccsid(*utf8);
    dcl-ds outstream likeds(stream_t) based(p_outstream);
    dcl-s buffer like(stream_buffer_t);
        
    
    assumedResult = loadFile('unittests/multipart/test4-umlaut-field-name.txt');
    
    p_outstream = stream_new(%size(stream_buffer_t));
    outstream.writer = %paddr(stream_memory_writer);
    outstream.handle = %addr(buffer);
    
    multipart = iv_multipart_new(IV_MULTIPART_MEDIA_TYPE : BOUNDARY);
    iv_multipart_addPartFromString(multipart : 'L' + u'00FC' + 'ckenf' + u'00FC' + 'ller' : 'ILEvator rocks!');
    iv_multipart_finalize(multipart);
    
    iv_multipart_toStream(multipart : p_outstream);
    
    iEqual(%len(assumedResult) : %len(buffer));
    assert(memcmp(%addr(assumedResult : *data) : %addr(buffer : *data) : %len(buffer)) = 0: 
        'Multipart does not correspond with the assumed result.');
    
    on-exit;
        iv_multipart_free(multipart);
        stream_delete(p_outstream);
end-proc;


dcl-proc test_singleTextPartWithHeaders export;
    dcl-s multipart pointer;
    dcl-s assumedResult varchar(1000000:4) ccsid(*utf8);
    dcl-ds outstream likeds(stream_t) based(p_outstream);
    dcl-s buffer like(stream_buffer_t);
    dcl-s headers pointer;
    
    assumedResult = loadFile('unittests/multipart/test2-single-text-part-with-additional-headers.txt');
    
    headers = iv_buildList(
        'User-Defined-1' : 'first part' : 
        'User-Defined-2' : 'second test'
    ); 
        
    p_outstream = stream_new(%size(stream_buffer_t));
    outstream.writer = %paddr(stream_memory_writer);
    outstream.handle = %addr(buffer);
    
    multipart = iv_multipart_new(IV_MULTIPART_MEDIA_TYPE : BOUNDARY);
    iv_multipart_addPartFromString(multipart : 'test' : 'ILEvator rocks!' : IV_MULTIPART_ENCODING_TEXT : headers);
    iv_multipart_finalize(multipart);
    
    iv_multipart_toStream(multipart : p_outstream);
    
    iEqual(%len(assumedResult) : %len(buffer));
    assert(memcmp(%addr(assumedResult : *data) : %addr(buffer : *data) : %len(buffer)) = 0: 
        'Multipart does not correspond with the assumed result.');
    
    on-exit;
        iv_multipart_free(multipart);
        stream_delete(p_outstream);
end-proc;


dcl-proc test_multipleTextParts export;
    dcl-s multipart pointer;
    dcl-s assumedResult varchar(1000000:4) ccsid(*utf8);
    dcl-ds outstream likeds(stream_t) based(p_outstream);
    dcl-s buffer like(stream_buffer_t);
        
    
    assumedResult = loadFile('unittests/multipart/test3-multiple-text-parts.txt');
    
    p_outstream = stream_new(%size(stream_buffer_t));
    outstream.writer = %paddr(stream_memory_writer);
    outstream.handle = %addr(buffer);
    
    multipart = iv_multipart_new(IV_MULTIPART_MEDIA_TYPE : BOUNDARY);
    iv_multipart_addPartFromString(multipart : 'test1' : 'ILEvator rocks!');
    iv_multipart_addPartFromString(multipart : 'test2' : 'ILEastic rocks!');
    iv_multipart_addPartFromString(multipart : 'test3' : 'noxDB rocks!');
    iv_multipart_finalize(multipart);
    
    iv_multipart_toStream(multipart : p_outstream);
    
    iEqual(%len(assumedResult) : %len(buffer));
    assert(memcmp(%addr(assumedResult : *data) : %addr(buffer : *data) : %len(buffer)) = 0: 
        'Multipart does not correspond with the assumed result.');
    
    on-exit;
        iv_multipart_free(multipart);
        stream_delete(p_outstream);
end-proc;


dcl-proc test_splitLinesOnBigFile export;
    dcl-c BASE64_INPUT_BLOCK_SIZE 466944;
    dcl-s assumedResult varchar(1000000:4) ccsid(*hex);
    dcl-s buffer varchar(1000000:4) ccsid(*hex);
    dcl-s input varchar(1000000:4) ccsid(*hex);
    dcl-s inputLength int(20);
    dcl-s block char(479232) ccsid(*hex);
    dcl-s splitLength uns(20);
    dcl-s length uns(20);
    
    input = loadFile('unittests/multipart/yellow-sky-base64.txt');
    assumedResult = loadFile('unittests/multipart/yellow-sky-base64-76.txt');
    
    inputLength = BASE64_INPUT_BLOCK_SIZE;
    block = %subst(input : 1 : inputLength);
    splitLength = splitLines(block : BASE64_INPUT_BLOCK_SIZE : *off);
    buffer = %subst(block : 1 : splitLength);
    
    inputLength = BASE64_INPUT_BLOCK_SIZE;
    block = %subst(input : BASE64_INPUT_BLOCK_SIZE + 1 : BASE64_INPUT_BLOCK_SIZE);
    length = splitLines(block : inputLength : *off);
    splitLength += length;
    buffer += %subst(block : 1 : length);
    
    inputLength = %len(input) - BASE64_INPUT_BLOCK_SIZE - BASE64_INPUT_BLOCK_SIZE;
    block = %subst(input : BASE64_INPUT_BLOCK_SIZE + BASE64_INPUT_BLOCK_SIZE + 1);
    length = splitLines(block : inputLength : *on);
    splitLength += length;
    buffer += %subst(block : 1 : length);
    
    // This is only for debug purposes:
    // writeFile('/tmp/ilevator-test-expected.txt' : %addr(assumedResult : *data) : %len(assumedResult));
    // writeFile('/tmp/ilevator-test-actual.txt' : %addr(buffer : *data) : %len(buffer));
    
    iEqual(%len(assumedResult) : %len(buffer));
    assert(memcmp(%addr(assumedResult : *data) : %addr(buffer : *data) : %len(buffer)) = 0: 
        'Multipart does not correspond with the assumed result.');
end-proc;


dcl-proc stream_memory_writer export;                               
    dcl-pi *n int(10);                                                 
        pStream pointer value;                                         
        buffer pointer value;                                          
        length uns(10) value;                                          
    end-pi;                                                            
                                                                       
    dcl-ds stream likeds(stream_t) based(pStream);                     
    dcl-s userBuffer like(stream_buffer_t) based(stream.handle);          
    dcl-s originalLength int(10);
                                               
    if (length > 0);
        originalLength = %len(userBuffer);
        %len(userBuffer) = originalLength + length;
        memcpy(%addr(userBuffer : *data) + originalLength : buffer : length);
    endif;                                                             
                                                                       
    return length;  
end-proc; 

dcl-proc loadFile;
  dcl-pi *n varchar(1000000) ccsid(*hex);
    p_path varchar(1000) const;
  end-pi;

  dcl-s path varchar(1000);
  dcl-s content char(1000000) ccsid(*hex);
  dcl-s returnValue varchar(1000000) ccsid(*hex);
  dcl-s len int(10);
  dcl-s fd int(10);

  path = projectSourceDir + p_path + x'00';

  fd = open(%addr(path : *data) : O_RDONLY);
  if (fd >= 0);
    len = read(fd : %addr(content) : 1000000);
    returnValue = %subst(content : 1 : len);
  endif;

  return returnValue;
  
  on-exit;
    if (fd >= 0);
      callp close(fd);
    endif;
end-proc;


dcl-proc writeFile;
  dcl-pi *n;
    p_path varchar(1000) const;
    buffer pointer const;
    length uns(10) const;
  end-pi;

  dcl-s path varchar(1000);
  dcl-s fd int(10);
  dcl-s len int(10);
  
  path = projectSourceDir + p_path + x'00';

  fd = open(%addr(path : *data) : O_WRONLY + O_CREATE : S_IRUSR + S_IWUSR);
  if (fd >= 0);
    len = write(fd : buffer : length);
  else;
    dsply 'Datei zum Schreiben nicht geöffnet.';
  endif;

  on-exit;
    if (fd >= 0);
      callp close(fd);
    endif;
end-proc;
